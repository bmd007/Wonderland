plugins {
	id 'org.springframework.boot' version '2.3.1.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'java'
	id 'idea'
	id "jacoco" //code coverage
}

jacoco {
	toolVersion = "0.8.5"
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

group = 'wonderland'

ext.buildNumber = {
	def buildNumber = System.getenv("VERSION")
	if (buildNumber == null || buildNumber.allWhitespace) {
		buildNumber = "SNAPSHOT"
	}
	return buildNumber
}
project.version = buildNumber()

sourceCompatibility = 14
targetCompatibility = 14

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', "Hoxton.SR5")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.cloud:spring-cloud-starter-config'
	implementation 'org.springframework.cloud:spring-cloud-starter-consul-discovery'
	implementation('com.jayway.jsonpath:json-path')

	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

	annotationProcessor 'org.projectlombok:lombok:1.18.12'
	implementation 'org.projectlombok:lombok:1.18.12'

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'io.projectreactor:reactor-test'
	testCompile("org.junit.jupiter:junit-jupiter-api")
	testRuntime("org.junit.jupiter:junit-jupiter-engine")
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

test {
	useJUnitPlatform()
}

wrapper {
	gradleVersion = "6.5"
	distributionType = Wrapper.DistributionType.ALL
}

bootJar {
	layered()
	layered {
		includeLayerTools = false
	}
}

bootBuildImage {
//	imageName = "bmd007/${project.name}:${project.version}"
	imageName = "bmd007/${project.name}"
	environment = ["BP_JVM_VERSION" : "14.0.1"]
}

springBoot {
	buildInfo()
}
